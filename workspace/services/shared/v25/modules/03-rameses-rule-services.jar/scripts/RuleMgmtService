import com.rameses.annotations.*;
import com.rameses.util.*;
import java.rmi.server.*;
import com.rameses.common.*
import com.rameses.services.extended.*;

class RuleMgmtService extends ActiveCrudListService {

	@ActiveDB("rule")
	def em;
	
	@Service("DateService")
	def dateSvc;

	@Service("TemplateService")
	def template;

	@Resource("RuleService")	
	def ruleSvc;

	@Env
	def env;

	@ProxyMethod
	public def getColumns( def o ) {
		return[
			[name:'name', caption:'Rule Name', width:250, resizable:false ],
			[name:'title', caption:'Title' ],
			[name:'rulegroup', caption:'Rule Group', width:150, resizable:false ],
			[name:'user.name', caption:'Author', width:150,  resizable:false],
			[name:'dtfiled', caption:'Date Created', width:150,  resizable:false ]
		];
	}
	
	public void beforeCreate(def o) {
		if(o.salience==null) o.salience = 0;
		if(!o.effectivefrom) o.effectivefrom = dateSvc.serverDate;
		o.dtfiled = dateSvc.serverDate;
		o.user = [objid:env.USERID, name:env.USER];
		o.state = 'DRAFT';
	}

	public Object open(Object o) {
		o = em.findRule( o );
		o.conditions = em.getRuleConditions(o);
		o.conditions.each { c->
			c.constraints = em.getRuleConditionConstraints(c);
			c.constraints.each {
				if(it.listvalue) it.listvalue = em.serializer.read(it.listvalue);
				if(!it.operator?.symbol) it.operator = null;
			}
		};
		o.actions = em.getRuleActions(o);
		o.actions.each { a->
			a.params = em.getRuleActionParams(a);
			a.params.each {
				if(it.listvalue) it.listvalue = em.serializer.read(it.listvalue);
			}
		}
		return o;
	}

	@ProxyMethod
	public def getRulegroups(o) {
		return em.getRulegroups( o );
	}
	
	/*********************************************
	* CONDITION
	**********************************************/
	@ProxyMethod
	public def getFacts(o) {
		return em.getFacts( o );
	}

	
	@ProxyMethod
	public def findFact(o) {
		def f = em.read( o, "fact" );
		f.fields = em.getFactFields(o);
		return f;
	}

	private void saveFactVariable( o ) {
		//we add the fact as vars 
		if( o.varname ) {
			def m = [
				objid: o.objid, 
				parentid: o.objid, 
				ruleid: o.parentid,
				varname: o.varname,
				datatype: o.fact.name,
				pos: o.pos
			];		
			em.save( m, "var");
		}
		else {
			em.delete( [objid:o.objid], "var");
		}
	}

	private void saveConstraintVariable(o, cond) {
		if( o.varname ) {
			def dtype = o.field.vardatatype;
			if( o.dynamicvar?.datatype ) dtype = o.dynamicvar.datatype;

			def z = [
				objid: o.objid,
				parentid: cond.objid,
				ruleid: cond.parentid,
				varname: o.varname,
				datatype: dtype,
				pos: o.pos
			];
			em.save( z, "var");
		}
		else {
			em.delete( [objid:o.objid], "var");
		}
	}



	@ProxyMethod
	public def saveCondition(o) {
		em.save( o, "condition" );
		saveFactVariable(o);

		int i = 0;
		o.constraints.each {
			if(!o.fieldname) it.fieldname = it.field.name;
			it.parentid = o.objid;
			em.save( it, "constraint");
			saveConstraintVariable( it, o );
		}
		o._deleted_constraints?.each {
			em.delete(it, "constraint");
			em.delete( [objid:it.objid], "var");
		}
	}

	@ProxyMethod
	public def findCondition(o) {
		def c = em.read( o, "condition" );
		c.vars = em.getRuleConditionVars(c);
		c.constraints = em.getRuleConditionConstraints(c);
		c.constraints.each {
			if(it.listvalue) it.listvalue = em.serializer.read(it.listvalue);
		}
		return c;
	}

	@ProxyMethod
	public def findAllVarsByType(def opt) {
		if(!opt.ruleid) throw new Exception("Please specify ruleid");

		def builder = new StringBuilder();
		if(opt.datatype) {
			if(opt.datatype == 'number') {
				builder.append( " AND (datatype='decimal' OR datatype='integer') " );
			}
			else if(opt.datatype == "obj") {
				builder.append( " AND NOT(datatype='decimal')" );
				builder.append( " AND NOT(datatype='integer')" );
				builder.append( " AND NOT(datatype='string')" );
			}
			else {
				builder.append( ''' AND datatype=$P{datatype}''' );
			}
		}
		if( opt.pos ) {
			builder.append( ''' AND pos < $P{pos} ''' );
		}
		opt.filter = builder.toString();
		return em.findAllVarsByType(opt);
	}


	/*********************************************
	* ACTION
	**********************************************/
	@ProxyMethod
	public def getActionDefs(o) {
		return em.getActionDefs( o );
	}

	@ProxyMethod
	public def findActionDef(o) {
		def a = em.read( o, "actiondef" );
		a.params = em.getActionDefParams(o);
		return a;
	}

	@ProxyMethod
	public def saveAction(o) {
		o.pos = 0;
		em.save( o, "action" );
		o.params.each {
			it.parentid = o.objid;
			it.pos = 0;
			em.save(it, "actionparam");
		}
	}

	@ProxyMethod
	public void removeCondition(o) {
		em.removeAllConditionConstraint( o );
		em.removeAllConditionVar(o);
		em.delete( o, "condition" );
	}

	@ProxyMethod
	public void removeAction(o) {
		em.removeAllActionParams( o );
		em.delete( o, "action" );
	}


	@ProxyMethod
	public def deploy( o ) {
		o = open(o);
		if(o.state=='DEPLOYED') 
			throw new Exception("Rule is already approved");

		//if(o.state!='APPROVED') 
		//	throw new Exception("Rule must be in approved state")
		o.vars = em.getRuleVars(o); 
		String ruletext =  template.get( "rules/rule", [rule: o, templateSvc: template ] );
		//println ruletext;
		//throw new Exception("alright stop");
		try {
			ruleSvc.addRulePackage( o.ruleset, new java.io.StringReader(ruletext) );
			em.create( [objid:o.objid,ruletext:ruletext], "deployed");
			o.state = 'DEPLOYED';
			em.update( o );
			return o;
		}
		catch(e) {
			println "----------ERROR--------"
			println ruletext;
			println e.message;
			println "------------------------"
			throw e;
		}
	}

	@ProxyMethod
	public def undeploy( o ) {
		o = em.read(o);
		String pkg = o.ruleset + "." + o.name; 
		ruleSvc.removeRulePackage( o.ruleset, pkg );
		em.delete( [objid:o.objid], "deployed" );
		o.state = 'APPROVED';
		em.update(o);
		return o;
	}


}
